import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow'
}

group = 'org.octopusden.octopus.components.registry.automation'
description = 'Octopus Components Registry Automation'

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        suppressWarnings = true
        jvmTarget = "1.8"
    }
}

java {
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    api project(":components-registry-service-client")
    implementation("com.github.ajalt.clikt:clikt:3.2.0")

    implementation("org.slf4j:slf4j-api:2.0.13")
    implementation("ch.qos.logback:logback-classic:1.3.14")
    implementation("io.github.openfeign:feign-core:12.2")

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

application {
    mainClass = "${group}.ApplicationKt"
}

tasks.jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

tasks.register("zipMetarunners", Zip) {
    archiveFileName = 'metarunners.zip'
    from(layout.projectDirectory.dir('metarunners')) {
        expand(project.properties)
    }
}

configurations {
    create("distributions")
}

def metarunners = artifacts.add(
        'distributions',
        layout.buildDirectory.file('distributions/metarunners.zip').get().asFile
) {
    classifier = 'metarunners'
    type = 'zip'
    builtBy 'zipMetarunners'
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri('https://ossrh-staging-api.central.sonatype.com/service/local/'))
            snapshotRepositoryUrl.set(uri('https://central.sonatype.com/repository/maven-snapshots/'))
            username.set(System.getenv('MAVEN_USERNAME'))
            password.set(System.getenv('MAVEN_PASSWORD'))
        }
    }
    transitionCheckOptions {
        maxRetries.set(60)
        delayBetween.set(Duration.ofSeconds(30))
    }
}

test {
    useJUnitPlatform()
}

tasks.named('distZip').configure { enabled = false }
tasks.named('shadowDistZip').configure { enabled = false }
tasks.named('distTar').configure { enabled = false }
tasks.named('shadowDistTar').configure { enabled = false }
