import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow'
    id 'com.avast.gradle.docker-compose'
}

group = 'org.octopusden.octopus.components.registry.automation'
description = 'Octopus Components Registry Automation'

repositories {
    mavenCentral()
}

dependencies {
    api project(":components-registry-service-client")
    implementation("com.github.ajalt.clikt:clikt:4.4.0")

    implementation("org.slf4j:slf4j-api:2.0.13")
    implementation("ch.qos.logback:logback-classic:1.3.14")
    implementation("io.github.openfeign:feign-core:12.2")

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

ext {
    def env = System.getenv()

    dockerRegistry = env.getOrDefault("DOCKER_REGISTRY", properties["docker.registry"]).toString()
}

tasks.named("composeUp") {
    dependsOn(":components-registry-service-server:dockerBuildImage")
}

dockerCompose {
    useComposeFiles.add("${project.projectDir}/docker/docker-compose.yaml")
    waitForTcpPorts = true
    captureContainersOutputToFiles = layout.buildDirectory.dir("docker-logs").get().asFile
    environment = [
            "DOCKER_REGISTRY"                    : project.ext.get("dockerRegistry"),
            "COMPONENTS_REGISTRY_SERVICE_VERSION": project.version
    ]
}

application {
    mainClass = "${group}.ApplicationKt"
}

tasks.jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

tasks.register("zipMetarunners", Zip) {
    archiveFileName = 'metarunners.zip'
    from(layout.projectDirectory.dir('metarunners')) {
        expand(project.properties)
    }
}

tasks.shadowJar {
    dependsOn composeUp
}

tasks.withType(Test) {
    dependsOn shadowJar
    systemProperties["test.components-registry-service-host"] = "localhost"
    systemProperties["test.components-registry-service-port"] = 4567
    useJUnitPlatform()
    testLogging {
        events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED
    }
    systemProperty "jar", configurations.shadow.artifacts.files.asPath
    finalizedBy composeLogs, composeDown
}

configurations {
    create("distributions")
}

def metarunners = artifacts.add(
        'distributions',
        layout.buildDirectory.file('distributions/metarunners.zip').get().asFile
) {
    classifier = 'metarunners'
    type = 'zip'
    builtBy 'zipMetarunners'
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri('https://ossrh-staging-api.central.sonatype.com/service/local/'))
            snapshotRepositoryUrl.set(uri('https://central.sonatype.com/repository/maven-snapshots/'))
            username.set(System.getenv('MAVEN_USERNAME'))
            password.set(System.getenv('MAVEN_PASSWORD'))
        }
    }
    transitionCheckOptions {
        maxRetries.set(60)
        delayBetween.set(Duration.ofSeconds(30))
    }
}

tasks.named('distZip').configure { enabled = false }
tasks.named('shadowDistZip').configure { enabled = false }
tasks.named('distTar').configure { enabled = false }
tasks.named('shadowDistTar').configure { enabled = false }
