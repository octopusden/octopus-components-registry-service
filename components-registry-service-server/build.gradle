//noinspection GroovyAssignabilityCheck
plugins {
    id 'org.springframework.boot'
    id 'org.jetbrains.kotlin.plugin.spring'
    id 'java-test-fixtures'
    id 'com.bmuschko.docker-java-application'
}

apply plugin: 'docker-compose'

dependencies {
    implementation(project(':component-resolver-core')) {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-main-kts'
    }
    implementation project(':components-registry-service-core')

    implementation platform("org.springframework.boot:spring-boot-dependencies:${project['spring-boot.version']}")
    implementation platform("org.springframework.cloud:spring-cloud-dependencies:${project.properties["spring-cloud.version"]}")

    implementation("org.springframework.cloud:spring-cloud-starter-config")
    implementation("org.springframework.cloud:spring-cloud-starter-bootstrap")
    implementation("org.springframework.retry:spring-retry")

    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.cloud:spring-cloud-starter-netflix-eureka-client")

    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-devtools"
    implementation 'io.micrometer:micrometer-registry-prometheus:1.2.1'
    implementation "io.github.microutils:kotlin-logging:1.6.22"

    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:${project.properties["openapi.version"]}")

    implementation("org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r")

    testImplementation "com.github.kittinunf.fuel:fuel:2.3.1"
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation testFixtures(project(":test-common"))
}

sourceSets {
    test {
        resources {
            srcDirs += [ project(':test-common').sourceSets.test.resources ]
        }
    }
    integrationTest {
        kotlin {
            srcDir 'src/integrationTest/kotlin'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    description = 'Runs integration tests including fat jar startup validation'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
    mustRunAfter bootJar
    
    // Pass required properties to test
    systemProperty 'fatJar.path', bootJar.archiveFile.get().asFile.absolutePath
    systemProperty 'test.resources.path', "$projectDir/src/integrationTest/resources"
    systemProperty 'java.home', System.getProperty('java.home')
    
    useJUnitPlatform()
    jvmArgs('--add-opens=java.base/java.lang=ALL-UNNAMED')
    jvmArgs('--add-opens=java.base/java.util=ALL-UNNAMED')
}

check.dependsOn integrationTest
integrationTest.dependsOn bootJar

// Integration test inherits testImplementation configuration which includes transitive dependencies with resources.
// Gradle's Copy task detects these as duplicates during resource processing.
processIntegrationTestResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jar {
    archiveBaseName = "${project.name}-it"
    enabled = true
    archiveClassifier = ''
}

bootJar  {
    requiresUnpack '**/kotlin-compiler-*.jar'
}

springBoot {
    buildInfo()
}

def dockerRegistry = System.getenv().getOrDefault("DOCKER_REGISTRY", project.properties["docker.registry"])
def octopusGithubDockerRegistry = System.getenv().getOrDefault("OCTOPUS_GITHUB_DOCKER_REGISTRY", project.properties["octopus.github.docker.registry"])

dockerCreateDockerfile.doLast {
    copy {
        from 'Dockerfile'
        into dockerSyncBuildContext.destinationDir
    }
}

tasks.named('dockerSyncBuildContext') {
    def bootJarArchive = tasks.named('bootJar').flatMap { it.archiveFile }
    from(bootJarArchive) {
        into('build/libs')
    }
    dependsOn bootJar
}

dockerBuildImage {
    inputDir = dockerSyncBuildContext.destinationDir
    buildArgs.put("DOCKER_REGISTRY", "$dockerRegistry")
    buildArgs.put("BUILD_VERSION", "${project.version}")
    images.set(["$octopusGithubDockerRegistry/octopusden/components-registry-service:${project.version}"])

    dependsOn dockerSyncBuildContext
    dependsOn bootJar
}

