//noinspection GroovyAssignabilityCheck
plugins {
    id 'org.springframework.boot'
    id 'org.jetbrains.kotlin.plugin.spring'
    id 'java-test-fixtures'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

configurations {
    [testCompileClasspath, testRuntimeClasspath].each { cfg ->
        // This parameter is necessary to support the ability to run tests.
        // In the future need to create separate ft module.
        cfg?.attributes?.attribute(org.gradle.api.attributes.java.TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 21)
        cfg?.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-main-kts'
    }
}

dependencies {
    api project(":components-registry-service-core")
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation(platform("io.github.openfeign:feign-bom:12.2"))
    implementation("io.github.openfeign:feign-httpclient")
    implementation("io.github.openfeign:feign-jackson")
    implementation("io.github.openfeign:feign-slf4j")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")

    testImplementation project(":components-registry-service-server")
    testImplementation testFixtures(project(":test-common"))
    testImplementation platform("org.springframework.boot:spring-boot-dependencies:${project['spring-boot-legacy.version']}")
    testImplementation ("org.springframework.boot:spring-boot-starter-test")

    testImplementation("org.assertj:assertj-core:3.21.0")
    testImplementation("ch.qos.logback:logback-classic:1.2.7")
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
}
sourceSets {
    test {
        resources {
            srcDirs += [ project(':test-common').sourceSets.test.resources ]
        }
    }
}
